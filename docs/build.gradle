buildscript {
    dependencies {
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11")
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

group 'sp-docs'
version '1.0-SNAPSHOT'

asciidoctor {
    backends = ['html5', 'pdf']

    sourceDir = file('src')
    outputDir = file('.')

    def files = '*.adoc'

    sources {
        include files
    }

    attributes       \
            'source-highlighter': 'coderay',
            'encoding': 'utf-8',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'idprefix': '',
            'idseparator': '-',
            'pdf-stylesdir': 'theme',
            'pdf-style': 'finki',
            'pdf-fontsdir': 'fonts'
}

task srcZip
task srcZipEn

(1..2).each { id ->
    task "srcZip${id}"(type: Zip) {
        from('../src/av' + id)
        excludes = ['*.out', '*_en.c']
        archiveName = "av${id}_src.zip"
        destinationDir = file('./sources')
    }
    srcZip.dependsOn "srcZip${id}"
}


(1..2).each { id ->
    task "srcZipEn${id}"(type: Zip) {
        from('../src/av' + id)
        excludes = ['*.out']
        includes = ['*_en.c']
        archiveName = "av${id}_src_en.zip"
        destinationDir = file('./sources')
    }
    srcZipEn.dependsOn "srcZipEn${id}"
}